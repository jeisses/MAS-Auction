extensions [table]

breed [buyers buyer]

; --- Buyer variables ---
;
; 1) beliefs
; 2) desire
; 3) intention
; 4) private-value: the agen't private value for a good (currently there is only 1 good)
; x) money: money of the agent
; 5) outgoing-messages
; 6) incomming-messages
buyers-own [beliefs desire intention private-value money outgoing-messages incoming-messages wishlist]

to setup-buyers
  create-buyers num-buyers [
    setxy 4 + min-pxcor + who * 3 min-pycor + 3
    set heading 90
    set color red
    set shape "person"
    
    ; Initial money is a setting
    set money initial-buyer-money
    
    ; Draw the buyer's private-values from a normal distribution 
    set private-value (round (random-normal 50 5))
    set label money

		set wishlist table:make
		table:put wishlist "wood" random 3 + 5

		set beliefs table:make
    
    ; Store the initial belifs
    table:put beliefs "money" money
    table:put beliefs "value" private-value
		table:put beliefs "wishlist" wishlist
		table:put beliefs "bought good" false

    set desire "minimize-price"
    set intention [""]
    set incoming-messages (list)
    set outgoing-messages (list)
  ]
end

to update-buyer-beliefs
  ask buyers [
    ;; Set outgoing-messages to an empty list
    set outgoing-messages (list)
    
    foreach incoming-messages [
      ;; Message with base prices of all goods. Format is:
      ;; ["price list" [p1 p2 p3 ...]]
      ifelse first ? = "price list" [
        table:put beliefs "price list" item 1 ?
      ][
      ;; Message about what good is being sold. Format is: 
      ;; ["auction" good-turtle-id current-bid]
      ifelse first ? = "auction" [
        ;; print ?
        table:put beliefs "auctioned good" item 1 ?
        table:put beliefs "current bid" item 2 ?
      ][
      ;; Message containing the current highest bid. Format is:
      ;; ["current bid" buyer-id price]
      ifelse first ? = "current bid" [
        table:put beliefs "current buyer" item 1 ?
        table:put beliefs "current bid" item 2 ?
      ][
			;; Message announcing sale
			;; ["deal" buyer_id price good_id auctioneer_id]
			ifelse first ? = "deal" and item 1 ? = who [
        set money round (money - item 2 ?)
        set label money
        table:put beliefs "money" money 
				let gtype ""
				ask good item 3 ? [ set gtype good-type ] 
				table:put wishlist gtype (table:get wishlist gtype - 1)
				table:put beliefs "bought good" (list item 3 ? item 4 ? item 2 ?)
        table:remove beliefs "auctioned good"
        table:remove beliefs "current buyer"
        table:remove beliefs "current bid"
			][
      ;; Auction for the current good is closed 
      ifelse first ? = "close auction" or first ? = "deal" [
        table:remove beliefs "auctioned good"
        table:remove beliefs "current bid"
        table:remove beliefs "current buyer"
      ][]]]]]
    ] 
    set incoming-messages []
  ]
  
end


to update-buyer-desires
	ask buyers [
		let done  true
		foreach table:keys wishlist [ if table:get wishlist ? > 0 [ set done false ]]
		if done [ set desire "shutdown" ]
	]
end

to update-buyer-intentions
  ask buyers [
    ;; Determine how much we want to bid on the current auction
    let bid false
    if table:has-key? beliefs "auctioned good" [
			let goodtype ""
			ask good table:get beliefs "auctioned good" [ set goodtype good-type ]

      if ((not (table:has-key? beliefs "current buyer")) or not ((table:get beliefs "current buyer") = who))
				 		 	and table:get wishlist goodtype > 0 [
        set bid (basic-bid-strategie (table:get beliefs "auctioned good") (table:get beliefs "current bid"))
      ]
    ]

		ifelse is-list? table:get beliefs "bought good"[
			set intention  fput "store good" table:get beliefs "bought good"
			table:put beliefs "bought good" false
		][
    ;; Bidding intentions contains the bid price
    ifelse is-number? bid [
      set intention (list "bid" bid)
    ][
    ;; Intention to pass the current round
    ifelse bid = false and (table:has-key? beliefs "auctioned good") or (table:get beliefs "money") <= 0 [
      set intention ["pass"]
    ][
    set intention [""]
    ]]]
  ]
end

to execute-buyer-actions
  ask buyers with [not empty? intention] [
    ;; Bidding action
    ifelse first intention = "bid" [
      ;; TODO: support multiple auctioneers
      ;;print (list "Bidding: " (item 1 intention))
      let buyer-id who
      set outgoing-messages fput (list "bid" buyer-id (item 1 intention))  outgoing-messages
    ][
    ;; Pass
    ifelse first intention = "pass" [
      set outgoing-messages fput (list "pass") outgoing-messages
    ][
		ifelse first intention = "store good" [
			let xc xcor
			let yc ycor
			
			ask good item 1 intention [ setxy xc yc - 2 ]
			;; message to selling auctioneer ["deal" auctioneer_id good_id price ]
			set outgoing-messages fput (list "deal" item 2 intention item 1 intention item 3 intention) outgoing-messages
		][]]
    ]
  ]
end

;--- Send messages ---
to send-buyer-messages
  ask buyers [
    let msgs outgoing-messages
    ask auctioneers [
      (foreach msgs [
         set incoming-messages fput ? incoming-messages 
      ])
    ]
  ]
end

;--- Buyer utility function. Call in buyer context. ---
to-report buyer-utility 
  let num-goods count goods-at 0 -2
  let goods-value table:get beliefs "value" * num-goods
  let num-wish table:get wishlist "wood"
  report money + goods-value - (num-wish * 30)
end

;;;;;;;;;;;;;;;;;;;;;
;; BUYER STRATEGIES
;;;;;;;;;;;;;;;;;;;;;

; --- Basic bidding strategy ---
;
; Place bid if private-value is higher than current-bid. Currently always
; bids when private value is larger than current-bid. Should be extended.
to-report basic-bid-strategie [good-id current-bid]
  let goodtype ""
  ask good good-id [set goodtype good-type]
  let wishvalue (table:get wishlist goodtype) / 100
  let max-bid table:get beliefs "value" 
  let bid min (list money (current-bid + wishvalue * money))
  
  if money > bid and bid <= max-bid and bid > current-bid [
    report bid
  ]
  report false
end